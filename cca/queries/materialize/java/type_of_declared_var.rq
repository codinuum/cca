
DEFINE input:inference "ont.cpi"

PREFIX ent:  <http://codinuum.com/fact/entity/>
PREFIX rel:  <http://codinuum.com/fact/version/release/>
PREFIX ext:  <http://codinuum.com/fact/external/>
PREFIX src:  <http://codinuum.com/ontologies/2012/10/source-code-entity#>
PREFIX ver:  <http://codinuum.com/ontologies/2012/10/versioning#>
PREFIX chg:  <http://codinuum.com/ontologies/2012/10/primitive-change#>
PREFIX java: <http://codinuum.com/ontologies/2012/10/java-entity#>
PREFIX c:    <http://codinuum.com/ontologies/2012/10/c-entity#>


INSERT {
  ?var java:typeName ?tyname .
  ?var java:typeDims ?dims .
}
WHERE {

  ?var a java:Name ;
       java:declaredBy ?dtor .
  
  ?dtor a java:VariableDeclarator ;
        java:inVariableDeclaration ?decl .

  ?ty a java:Type ;
      src:parent ?decl .
  {
    ?ty a java:BooleanType .
    BIND ("boolean" AS ?tyname)
  }
  UNION
  {
    ?ty a java:DoubleType .
    BIND ("double" AS ?tyname)
  }
  UNION
  {
    ?ty a java:FloatType .
    BIND ("float" AS ?tyname)
  }
  UNION
  {
    ?ty a java:ByteType .
    BIND ("byte" AS ?tyname)
  }
  UNION
  {
    ?ty a java:CharType .
    BIND ("char" AS ?tyname)
  }
  UNION
  {
    ?ty a java:IntType .
    BIND ("int" AS ?tyname)
  }
  UNION
  {
    ?ty a java:LongType .
    BIND ("long" AS ?tyname)
  }
  UNION
  {
    ?ty a java:ShortType .
    BIND ("short" AS ?tyname)
  }
  UNION
  {
    ?ty java:name ?tyname .
  }

  OPTIONAL {
    ?ty java:dimensions ?dims .
  }

}
