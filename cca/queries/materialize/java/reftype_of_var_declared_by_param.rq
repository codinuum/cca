
DEFINE input:inference "ont.cpi"

PREFIX ent:  <http://codinuum.com/fact/entity/>
PREFIX rel:  <http://codinuum.com/fact/version/release/>
PREFIX ext:  <http://codinuum.com/fact/external/>
PREFIX src:  <http://codinuum.com/ontologies/2012/10/source-code-entity#>
PREFIX ver:  <http://codinuum.com/ontologies/2012/10/versioning#>
PREFIX chg:  <http://codinuum.com/ontologies/2012/10/primitive-change#>
PREFIX java: <http://codinuum.com/ontologies/2012/10/java-entity#>
PREFIX c:    <http://codinuum.com/ontologies/2012/10/c-entity#>


INSERT {
  ?var java:ofReferenceType ?TD .
}
WHERE {

  {
    SELECT DISTINCT ?var ?param ?src
    WHERE {

      ?var a java:Name ;
           java:inMethodOrConstructor ?meth ;
           java:name ?vname ;
           java:declaredBy ?param .

      ?meth a java:MethodOrConstructor ;
            java:inTypeDeclaration ?tdecl .

      ?tdecl a java:TypeDeclaration ;
             src:inSourceTree ?src .

      ?param a java:Parameter ;
             java:name ?pname .

    } GROUP BY ?var ?param ?src
  }

  ?TD a java:TypeDeclaration ;
      src:inSourceTree ?src ;
      java:fullyQualifiedName ?tyname .

  ?ty a java:ReferenceType ;
      src:parent ?param ;
      java:name ?tyname .

}
