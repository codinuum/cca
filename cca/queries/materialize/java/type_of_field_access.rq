
DEFINE input:inference "ont.cpi"

PREFIX ent:  <http://codinuum.com/fact/entity/>
PREFIX rel:  <http://codinuum.com/fact/version/release/>
PREFIX ext:  <http://codinuum.com/fact/external/>
PREFIX src:  <http://codinuum.com/ontologies/2012/10/source-code-entity#>
PREFIX ver:  <http://codinuum.com/ontologies/2012/10/versioning#>
PREFIX chg:  <http://codinuum.com/ontologies/2012/10/primitive-change#>
PREFIX java: <http://codinuum.com/ontologies/2012/10/java-entity#>
PREFIX c:    <http://codinuum.com/ontologies/2012/10/c-entity#>


INSERT {
  ?x java:typeName ?tyname ;
     java:typeDims ?dims ;
     java:declaredBy ?vdtor .
}
USING <http://codinuum.com/ont/cpi>
WHERE {

  FILTER NOT EXISTS {
    ?x java:typeName [] .
  }

  {
    SELECT DISTINCT ?x ?fname ?vdtor ?ty #?ver ?tdecl ?field
    WHERE {

      {
        SELECT DISTINCT ?x ?fname ?field ?vdtor #?ver ?tdecl
        WHERE {

          {
            SELECT DISTINCT ?x ?fname ?tdecl #?ver
            WHERE {

              {
                SELECT DISTINCT ?x ?fname ?tdecl ?ver
                WHERE {

                  {
                    SELECT DISTINCT ?x ?fname ?tdecl0 ?ver ?tdecl1
                    WHERE {

                      {
                        SELECT DISTINCT ?x ?fname ?tdecl0 ?ver ?tdecl1
                        WHERE {

                          {
                            SELECT DISTINCT ?x ?fname ?tdecl0 ?ver
                            WHERE {

                              {
                                SELECT DISTINCT ?x ?fname ?tdecl0
                                WHERE {

                                  ?x a java:FieldAccess ;
                                     java:name ?fname ;
                                     src:child0 ?e .

                                  ?e a java:Expression ;
                                     java:ofReferenceType ?tdecl0 .

                                } GROUP BY ?x ?fname ?tdecl0
                              }

                              ?tdecl0 ver:version ?ver .

                            } GROUP BY ?x ?fname ?tdecl0 ?ver
                          }

                          ?tdecl0 java:inTypeDeclaration* ?tdecl1 .

                        } GROUP BY ?x ?fname ?tdecl0 ?ver ?tdecl1
                      }

                      ?tdecl1 ver:version ?ver .

                    } GROUP BY ?x ?fname ?tdecl0 ?ver ?tdecl1
                  }

                  ?tdecl1 java:subTypeOf ?tdecl OPTION (TRANSITIVE,
                                                        T_DISTINCT,
                                                        #T_SHORTEST_ONLY,
                                                        T_NO_CYCLES,
                                                        T_MIN(0)) .
                } GROUP BY ?x ?fname ?tdecl ?ver
              }

              ?tdecl ver:version ?ver .

            } GROUP BY ?x ?fname ?tdecl #?ver
          }

          ?field a java:FieldDeclaration ;
                 src:child2 ?vdtor ;
                 java:inTypeDeclaration ?tdecl .

          FILTER EXISTS {
            ?vdtor java:name ?fname .
          }

        } GROUP BY ?x ?fname ?field ?vdtor #?ver ?tdecl
      }

      ?field src:child1 ?ty .

    } GROUP BY ?x ?fname ?vdtor ?ty #?ver ?tdecl ?field
  }

  ?ty a java:Type ;
      a ?cat OPTION (INFERENCE NONE) .

  {
    ?cat rdfs:label ?tyname0 .
    BIND (STR(?tyname0) AS ?tyname)
  }
  UNION
  {
    ?ty java:name ?tyname .
  }

  OPTIONAL {
    ?ty java:dimensions ?dims .
  }

}
