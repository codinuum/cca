
DEFINE input:inference "ont.cpi"

PREFIX ent:  <http://codinuum.com/fact/entity/>
PREFIX rel:  <http://codinuum.com/fact/version/release/>
PREFIX ext:  <http://codinuum.com/fact/external/>
PREFIX src:  <http://codinuum.com/ontologies/2012/10/source-code-entity#>
PREFIX ver:  <http://codinuum.com/ontologies/2012/10/versioning#>
PREFIX chg:  <http://codinuum.com/ontologies/2012/10/primitive-change#>
PREFIX java: <http://codinuum.com/ontologies/2012/10/java-entity#>
PREFIX c:    <http://codinuum.com/ontologies/2012/10/c-entity#>


INSERT {
  ?param java:typeName ?tyname .
  ?param java:typeDims ?dims .
}
USING <http://codinuum.com/ont/cpi>
WHERE {

  {
    SELECT DISTINCT ?param ?pname ?ty
    WHERE {
      ?param a java:Parameter ;
             java:name ?pname ;
             src:child1 ?ty .
    } GROUP BY ?param ?pname ?ty
  }

  ?ty a java:Type ;
      a ?cat OPTION (INFERENCE NONE) ;
      src:parent ?param .
  {
    ?cat rdfs:label ?tyname0 .
    BIND (STR(?tyname0) AS ?tyname)
  }
  UNION
  {
    ?ty java:name ?tyname .
  }

  OPTIONAL {
    ?ty java:dimensions ?dims .
  }

}
