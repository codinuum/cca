
DEFINE input:inference "ont.cpi"

PREFIX ent:  <http://codinuum.com/fact/entity/>
PREFIX rel:  <http://codinuum.com/fact/version/release/>
PREFIX ext:  <http://codinuum.com/fact/external/>
PREFIX src:  <http://codinuum.com/ontologies/2012/10/source-code-entity#>
PREFIX ver:  <http://codinuum.com/ontologies/2012/10/versioning#>
PREFIX chg:  <http://codinuum.com/ontologies/2012/10/primitive-change#>
PREFIX java: <http://codinuum.com/ontologies/2012/10/java-entity#>
PREFIX c:    <http://codinuum.com/ontologies/2012/10/c-entity#>

INSERT {
  ?ivk java:mayInvoke ?sig .
  ?ivk java:mayInvokeMethod ?meth .
}
WHERE {

  FILTER NOT EXISTS {
    ?ivk java:mayInvoke [] ;
         java:mayInvokeMethod [] .
  }

  VALUES ?nparams {1 2 3 4 5 6 7 8 9 10 11 12 13 14 15 }

  {
    SELECT DISTINCT ?mname ?tdecl ?ver ?meth ?mfqn ?msig ?nparams ?ivk ?args ?nargs
    WHERE {

      {
        SELECT DISTINCT ?mname ?meth ?mfqn ?msig ?nparams ?tdecl ?ver ?ivk
        WHERE {

          {
            SELECT DISTINCT ?mname ?meth ?mfqn ?msig ?nparams ?tdecl ?ver ?ivk
            WHERE {

              {
                SELECT DISTINCT ?mname ?meth ?mfqn ?msig ?nparams ?tdecl ?ver
                WHERE {

                  {
                    SELECT DISTINCT ?mname ?tdecl0 ?meth ?mfqn ?msig ?nparams ?tdecl1 ?ver
                    WHERE {

                  {
                    SELECT DISTINCT ?mname ?tdecl0 ?meth ?mfqn ?msig ?nparams ?ver
                    WHERE {

                      ?meth a java:MethodDeclaration ;
                            java:name ?mname ;
                            java:nParameters ?nparams ;
                            java:inTypeDeclaration ?tdecl0 ;
                            #src:child5 ?body ;
                            java:fullyQualifiedName ?mfqn ;
                            java:signature ?msig .

                      ?tdecl0 ver:version ?ver .

                    } GROUP BY ?mname ?tdecl0 ?meth ?mfqn ?msig ?nparams ?ver
                  }

                  ?tdecl1 java:subTypeOf ?tdecl0 OPTION (TRANSITIVE,
                                                         T_DISTINCT,
                                                         #T_SHORTEST_ONLY,
                                                         T_NO_CYCLES,
                                                         T_MIN(0)) .

                      ?tdecl1 ver:version ?ver .

                    } GROUP BY ?mname ?tdecl0 ?meth ?mfqn ?msig ?nparams ?tdecl1 ?ver
                  }

                  {
                    ?tdecl1 java:fullyQualifiedName ?tfqn .
                    BIND (?tdecl1 as ?tdecl)
                  } UNION {
                    ?tdecl a java:TypeDeclaration ;
                           java:inTypeDeclaration+ ?tdecl1 ;
                           java:fullyQualifiedName ?tfqn ;
                           ver:version ?ver .
                  }
                  # ?tdecl java:inTypeDeclaration* ?tdecl1 .
                  # ?tdecl ver:version ?ver .

                } GROUP BY ?mname ?meth ?mfqn ?msig ?nparams ?tdecl ?ver
              }

              ?e a java:Primary ;
                 src:parent ?ivk ;
                 java:ofReferenceType ?tdecl .

            } GROUP BY ?mname ?meth ?mfqn ?msig ?nparams ?tdecl ?ver ?ivk
          }

          FILTER EXISTS {
            ?ivk java:name ?mname .
          }

          FILTER (EXISTS {
            ?ivk a java:PrimaryMethodInvocation .
          } || EXISTS {
            ?ivk a java:PrimaryMethodInvocationStatement .
          })

        } GROUP BY ?mname ?meth ?mfqn ?msig ?nparams ?tdecl ?ver ?ivk
      }

      ?ivk java:nArguments ?nargs ;
           src:child2 ?args .

      ?args a java:Arguments .

    } GROUP BY ?mname ?tdecl ?ver ?meth ?mfqn ?msig ?nparams ?ivk ?args ?nargs
  }

  FILTER NOT EXISTS {
    GRAPH <http://codinuum.com/ont/cpi> {
      ?p_childx rdfs:subPropertyOf src:child .
    }
    BIND (STRAFTER(STR(?p_childx), "child") AS ?x)
    BIND (fn:number(?x) AS ?xn)
    FILTER (?xn < ?nparams)

    BIND (IRI(CONCAT(STR(java:parameterTypeName), ?x)) AS ?p_param_ty_namex)
    BIND (IRI(CONCAT(STR(java:parameterTypeDims), ?x)) AS ?p_param_ty_dimsx)

    ?args ?p_childx ?ax .
    ?meth ?p_param_ty_namex ?ptynamex .
    OPTIONAL {
      ?meth ?p_param_ty_dimsx ?ptydimsx .
    }
    OPTIONAL {
      ?ax java:typeDims ?atydimsx .
    }

    FILTER ((NOT EXISTS {
      ?ax java:typeName ?atynamex .
      FILTER (?atynamex = ?ptynamex)
    } && NOT EXISTS {
      ?ptyx a java:TypeDeclaration ;
            ver:version ?ver ;
            java:fullyQualifiedName ?ptynamex .
      ?ax java:ofReferenceType ?atyx .
      ?atyx java:subTypeOf ?ptyx OPTION (TRANSITIVE, T_DISTINCT, T_MIN(0)) ;
            ver:version ?ver .
    } && ?ptynamex != "java.lang.Object") ||
    (BOUND(?atydimsx) && ?atydimsx != ?ptydimsx))
  }

  FILTER (?nparams = ?nargs || (?nargs = ?nparams - 1 && EXISTS {
    ?meth java:isVariableArityMethod "true" .
  }) || (?nparams < ?nargs && EXISTS {
    ?meth java:isVariableArityMethod "true" .
  } && NOT EXISTS {

    BIND (STR(?nparams-1) AS ?lastp)
    BIND (IRI(CONCAT(STR(java:parameterTypeName), ?lastp)) AS ?p_param_ty_namex)
    BIND (IRI(CONCAT(STR(java:parameterTypeDims), ?lastp)) AS ?p_param_ty_dimsx)

    GRAPH <http://codinuum.com/ont/cpi> {
      ?p_childx rdfs:subPropertyOf src:child .
    }
    BIND (STRAFTER(STR(?p_childx), "child") AS ?x)
    BIND (fn:number(?x) AS ?xn)
    FILTER (?xn >= ?nparams && ?xn < ?nargs)

    ?args ?p_childx ?ax .
    ?meth ?p_param_ty_namex ?ptynamex .
    OPTIONAL {
      ?meth ?p_param_ty_dimsx ?ptydimsx .
    }
    OPTIONAL {
      ?ax java:typeDims ?atydimsx .
    }

    FILTER ((NOT EXISTS {
      ?ax java:typeName ?atynamex .
      FILTER (?atynamex = ?ptynamex)
    } && NOT EXISTS {
      ?ptyx a java:TypeDeclaration ;
            ver:version ?ver ;
            java:fullyQualifiedName ?ptynamex .
      ?ax java:ofReferenceType ?atyx .
      ?atyx java:subTypeOf ?ptyx OPTION (TRANSITIVE, T_DISTINCT, T_MIN(0)) ;
            ver:version ?ver .
    } && ?ptynamex != "java.lang.Object") ||
    (BOUND(?atydimsx) && ?atydimsx != ?ptydimsx))
  }))

  BIND (CONCAT(?mfqn, ?msig) AS ?sig)

}
