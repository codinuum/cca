
DEFINE input:inference "ont.cpi"

PREFIX ent:  <http://codinuum.com/fact/entity/>
PREFIX rel:  <http://codinuum.com/fact/version/release/>
PREFIX ext:  <http://codinuum.com/fact/external/>
PREFIX src:  <http://codinuum.com/ontologies/2012/10/source-code-entity#>
PREFIX ver:  <http://codinuum.com/ontologies/2012/10/versioning#>
PREFIX chg:  <http://codinuum.com/ontologies/2012/10/primitive-change#>
PREFIX java: <http://codinuum.com/ontologies/2012/10/java-entity#>
PREFIX c:    <http://codinuum.com/ontologies/2012/10/c-entity#>

INSERT {
  ?ivk java:mayInvoke ?sig .
  ?ivk java:mayInvokeMethod ?meth .
}
WHERE {

  FILTER NOT EXISTS {
    ?ivk java:mayInvoke [] ;
         java:mayInvokeMethod [] .
  }

  {
    SELECT DISTINCT ?uqn ?mname ?tdecl ?ver ?meth ?sig
    WHERE {

      ?meth a java:ConstructorDeclaration ;
            java:nParameters 0 ;
            java:name ?mname0 ;
            java:inTypeDeclaration ?tdecl ;
            java:fullyQualifiedName ?fqn ;
            java:signature ?sig0 .

      ?tdecl ver:version ?ver ;
             java:name ?tname ;
             java:fullyQualifiedName ?tfqn .

      BIND (CONCAT(?fqn, ?sig0) AS ?sig)
      BIND (CONCAT(?tfqn, ".", ?mname0) AS ?mname)
      BIND (CONCAT(?tname, ".", ?mname0) AS ?uqn)

    } GROUP BY ?uqn ?mname ?tdecl ?ver ?meth ?sig
  }

  {
    ?ivk a java:InstanceCreation ;
         java:inTypeDeclaration/ver:version ?ver ;
         java:nArguments 0 ;
         java:name ?mname .
  }
  UNION
  {
    SELECT DISTINCT ?ivk ?uqn ?ver ?mname
    WHERE {

      ?ivk a java:InstanceCreation ;
           java:inTypeDeclaration ?tdecl0 ;
           java:nArguments 0 ;
           java:name ?uqn .

      ?tdecl0 ver:version ?ver ;
              src:inFile ?file .

      {
        ?pkg a java:PackageDeclaration ;
             src:parent/src:inFile ?file ;
             java:name ?pname .
      }
      UNION
      {
        ?import a java:TypeImportOnDemandDeclaration ;
                src:parent/src:parent/src:inFile ?file ;
                java:name ?pname .
      }
      BIND (CONCAT(?pname, ".", ?uqn) AS ?mname)

    } GROUP BY ?ivk ?uqn ?ver ?mname
  }
  UNION
  {
    SELECT DISTINCT ?ivk ?uqn ?ver ?mname
    WHERE {

      {
        SELECT DISTINCT ?ivk ?uqn ?iname ?tdecl0 ?ver ?file
        WHERE {

          ?ivk a java:InstanceCreation ;
               java:inTypeDeclaration ?tdecl0 ;
               java:nArguments 0 ;
               java:name ?iname ;
               java:name ?uqn .

          ?tdecl0 ver:version ?ver ;
                  src:inFile ?file .

        } GROUP BY ?ivk ?uqn ?iname ?tdecl0 ?ver ?file
      }

      ?tdecl0 java:subClassOf+ ?tdecl1 .

      ?tdecl1 ver:version ?ver .

      {
        SELECT DISTINCT ?cname ?tdecl1
        WHERE {
          ?class a java:TypeDeclaration ;
                 java:inTypeDeclaration ?tdecl1 ;
                 java:name ?cname .

        } GROUP BY ?cname ?tdecl1
      }

      BIND (CONCAT(?cname, ".<init>") AS ?iname)

      ?pkg a java:PackageDeclaration ;
           src:parent/src:inFile ?file ;
           java:name ?pname .

      ?tdecl1 java:name ?tname1 .

      BIND (CONCAT(?pname, ".", ?tname1, "$$", ?uqn) AS ?mname)

    } GROUP BY ?ivk ?uqn ?ver ?mname
  }

}
