
DEFINE input:inference "ont.cpi"

PREFIX ent:  <http://codinuum.com/fact/entity/>
PREFIX rel:  <http://codinuum.com/fact/version/release/>
PREFIX ext:  <http://codinuum.com/fact/external/>
PREFIX src:  <http://codinuum.com/ontologies/2012/10/source-code-entity#>
PREFIX ver:  <http://codinuum.com/ontologies/2012/10/versioning#>
PREFIX chg:  <http://codinuum.com/ontologies/2012/10/primitive-change#>
PREFIX java: <http://codinuum.com/ontologies/2012/10/java-entity#>
PREFIX c:    <http://codinuum.com/ontologies/2012/10/c-entity#>

INSERT {
  ?meth ?p_param_ty_namex ?tyname .
  ?meth ?p_param_ty_dimsx ?dims .
}
USING <http://codinuum.com/ont/cpi>
WHERE {

  VALUES ?n { 0 1 2 3 4 5 6 7 8 9 10 11 12 13 14 15 }


  ?meth a java:MethodOrConstructor ;
        src:child2|src:child3 ?params .

  BIND (STR(?n) AS ?nstr)
  BIND (IRI(CONCAT(STR(src:child), ?nstr)) AS ?p_childx)

  BIND (IRI(CONCAT(STR(java:parameterTypeName), ?nstr)) AS ?p_param_ty_namex)
  BIND (IRI(CONCAT(STR(java:parameterTypeDims), ?nstr)) AS ?p_param_ty_dimsx)

  ?params a java:Parameters ;
          ?p_childx ?param .

  ?param a java:Parameter ;
         src:child1 ?pty .

  {
    ?pty java:name ?tyname .
  }
  UNION
  {
    ?pty a java:PrimitiveType ;
         a ?cat OPTION (INFERENCE NONE) .
    GRAPH <http://codinuum.com/ont/cpi> {
      ?cat rdfs:label ?lab
    }
    BIND(STR(?lab) AS ?tyname)
  }

  OPTIONAL {
    ?param java:dimensions ?dims0 .
  }

  OPTIONAL {
    ?pty java:dimensions ?dims1 .
  }

  BIND (COALESCE(?dims0, 0) + COALESCE(?dims1, 0) AS ?dims)

}
