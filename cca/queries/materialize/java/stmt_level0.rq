
DEFINE input:inference "ont.cpi"

PREFIX ent:  <http://codinuum.com/fact/entity/>
PREFIX rel:  <http://codinuum.com/fact/version/release/>
PREFIX ext:  <http://codinuum.com/fact/external/>
PREFIX src:  <http://codinuum.com/ontologies/2012/10/source-code-entity#>
PREFIX ver:  <http://codinuum.com/ontologies/2012/10/versioning#>
PREFIX chg:  <http://codinuum.com/ontologies/2012/10/primitive-change#>
PREFIX java: <http://codinuum.com/ontologies/2012/10/java-entity#>
PREFIX c:    <http://codinuum.com/ontologies/2012/10/c-entity#>


INSERT {
  ?stmt java:stmtLevel 0 .
}
WHERE {

  ?stmt a java:Statement ;
        java:inTypeDeclaration/src:inFile/src:location ?loc ;
        src:parent ?p .

  ?p a ?catp OPTION (INFERENCE NONE) .

  FILTER (?catp IN (java:MethodBody,java:ConstructorBody,java:StaticInitializer))

  FILTER (EXISTS {
    [] chg:mappedTo ?stmt .
  } || EXISTS {
    ?stmt chg:mappedTo [] .
  })

}
