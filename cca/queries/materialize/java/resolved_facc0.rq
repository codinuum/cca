
DEFINE input:inference "ont.cpi"

PREFIX ent:  <http://codinuum.com/fact/entity/>
PREFIX rel:  <http://codinuum.com/fact/version/release/>
PREFIX ext:  <http://codinuum.com/fact/external/>
PREFIX src:  <http://codinuum.com/ontologies/2012/10/source-code-entity#>
PREFIX ver:  <http://codinuum.com/ontologies/2012/10/versioning#>
PREFIX chg:  <http://codinuum.com/ontologies/2012/10/primitive-change#>
PREFIX java: <http://codinuum.com/ontologies/2012/10/java-entity#>
PREFIX c:    <http://codinuum.com/ontologies/2012/10/c-entity#>

INSERT {
  ?x a java:FieldAccess .
}
WHERE {

  ?x a java:AmbiguousName ;
     java:inTypeDeclaration ?tdecl ;
     java:name ?xname .

  FILTER NOT EXISTS {
    ?x java:identifier [] ;
       java:qualifier [] .
  }

  ?tdecl java:subTypeOf+ ?tdecl0 ;
         ver:version ?ver ;
         java:fullyQualifiedName ?cfqn .

  ?tdecl0 ver:version ?ver .

  ?field a java:FieldDeclaration ;
         java:inTypeDeclaration ?tdecl0 .

  ?vdtor a java:VariableDeclarator ;
         java:inField ?field ;
         java:name ?xname .

  FILTER NOT EXISTS {
    ?x a java:FieldAccess OPTION (INFERENCE NONE) .
  }
  FILTER NOT EXISTS {
    ?x a java:Name OPTION (INFERENCE NONE) .
  }

}
