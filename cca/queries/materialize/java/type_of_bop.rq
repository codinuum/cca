
DEFINE input:inference "ont.cpi"

PREFIX ent:  <http://codinuum.com/fact/entity/>
PREFIX rel:  <http://codinuum.com/fact/version/release/>
PREFIX ext:  <http://codinuum.com/fact/external/>
PREFIX src:  <http://codinuum.com/ontologies/2012/10/source-code-entity#>
PREFIX ver:  <http://codinuum.com/ontologies/2012/10/versioning#>
PREFIX chg:  <http://codinuum.com/ontologies/2012/10/primitive-change#>
PREFIX java: <http://codinuum.com/ontologies/2012/10/java-entity#>
PREFIX c:    <http://codinuum.com/ontologies/2012/10/c-entity#>


INSERT {
  ?x java:typeName ?tyname .
  ?x java:typeDims ?dims .
}
WHERE {

  ?x a java:BinaryOp ;
     src:child0 ?left ;
     src:child1 ?right .

  {
    ?left java:typeName ?tyname .
    OPTIONAL {
      ?left java:typeDims ?dims .
    }
  }
  UNION
  {
    ?right java:typeName ?tyname .
    OPTIONAL {
      ?right java:typeDims ?dims .
    }
  }

  FILTER NOT EXISTS {
    ?x java:typeName [] .
  }

}
