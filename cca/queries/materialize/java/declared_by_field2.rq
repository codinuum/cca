
DEFINE input:inference "ont.cpi"

PREFIX ent:  <http://codinuum.com/fact/entity/>
PREFIX rel:  <http://codinuum.com/fact/version/release/>
PREFIX ext:  <http://codinuum.com/fact/external/>
PREFIX src:  <http://codinuum.com/ontologies/2012/10/source-code-entity#>
PREFIX ver:  <http://codinuum.com/ontologies/2012/10/versioning#>
PREFIX chg:  <http://codinuum.com/ontologies/2012/10/primitive-change#>
PREFIX java: <http://codinuum.com/ontologies/2012/10/java-entity#>
PREFIX c:    <http://codinuum.com/ontologies/2012/10/c-entity#>


INSERT {
  ?x java:declaredBy ?vdtor .
}
WHERE {

  FILTER NOT EXISTS {
    ?x java:declaredBy [] .
  }

  {
    SELECT DISTINCT ?vdtor ?fname ?ffqn ?ffqnr ?tdecl
    WHERE {

      ?vdtor a java:VariableDeclarator ;
             src:parent ?fdecl ;
             java:name ?fname .

      ?fdecl a java:FieldDeclaration ;
             java:inTypeDeclaration ?tdecl .

      ?tdecl a java:TypeDeclaration ;
             java:fullyQualifiedName ?cfqn .

      BIND (CONCAT(?cfqn, ".", ?fname) AS ?ffqn)
      BIND (CONCAT(REPLACE(?cfqn, "[$$]", "."), ".", ?fname) AS ?ffqnr)

    } GROUP BY ?vdtor ?fname ?ffqn ?ffqnr ?tdecl
  }

  ?x a java:Name ;
       java:name ?name .

  FILTER (?name IN (?ffqn,?ffqnr))

  FILTER EXISTS {
    ?tdecl ver:version ?ver .
    ?x java:inTypeDeclaration/ver:version ?ver .
  }

}
