
DEFINE input:inference "ont.cpi"

PREFIX ent:  <http://codinuum.com/fact/entity/>
PREFIX rel:  <http://codinuum.com/fact/version/release/>
PREFIX ext:  <http://codinuum.com/fact/external/>
PREFIX src:  <http://codinuum.com/ontologies/2012/10/source-code-entity#>
PREFIX ver:  <http://codinuum.com/ontologies/2012/10/versioning#>
PREFIX chg:  <http://codinuum.com/ontologies/2012/10/primitive-change#>
PREFIX java: <http://codinuum.com/ontologies/2012/10/java-entity#>
PREFIX c:    <http://codinuum.com/ontologies/2012/10/c-entity#>


INSERT {
  ?x java:declaredBy ?vdtor .
}
WHERE {

  FILTER NOT EXISTS {
    ?x java:declaredBy [] .
  }

  {
    SELECT DISTINCT ?x ?fname ?tdecl0
    WHERE {

      ?x a java:FieldAccess ;
         java:name ?fname ;
         java:inTypeDeclaration ?tdecl0 .

      FILTER (EXISTS {
        ?x src:child0 [ a java:This ] .
      } || NOT EXISTS {
        ?x src:child0 [] .
      })

    } GROUP BY ?x ?fname ?tdecl0
  }

  # ?tdecl0 (java:inTypeDeclaration|java:subTypeOf) ?tdecl OPTION (TRANSITIVE,
  #                                                                T_DISTINCT,
  #                                                                T_SHORTEST_ONLY,
  #                                                                T_NO_CYCLES,
  #                                                                T_MIN(0)) .

  ?fdecl a java:FieldDeclaration ;
         src:child2 ?vdtor ;
         java:inTypeDeclaration ?tdecl .

  ?vdtor a java:VariableDeclarator ;
         src:parent ?fdecl ;
         java:name ?fname .

  ?tdecl ver:version ?ver .
  ?tdecl0 ver:version ?ver .

  FILTER EXISTS {
    ?tdecl0 java:inTypeDeclaration ?tdecl1 OPTION (TRANSITIVE,
                                                   T_DISTINCT,
                                                   #T_SHORTEST_ONLY,
                                                   T_NO_CYCLES,
                                                   T_MIN(0)) .

    ?tdecl1 java:subTypeOf ?tdecl OPTION (TRANSITIVE,
                                          T_DISTINCT,
                                          T_SHORTEST_ONLY,
                                          T_NO_CYCLES,
                                          T_MIN(0)) .
  }

}
