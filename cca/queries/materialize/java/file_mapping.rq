
DEFINE input:inference "ont.cpi"

PREFIX ent:  <http://codinuum.com/fact/entity/>
PREFIX rel:  <http://codinuum.com/fact/version/release/>
PREFIX ext:  <http://codinuum.com/fact/external/>
PREFIX src:  <http://codinuum.com/ontologies/2012/10/source-code-entity#>
PREFIX ver:  <http://codinuum.com/ontologies/2012/10/versioning#>
PREFIX chg:  <http://codinuum.com/ontologies/2012/10/primitive-change#>
PREFIX java: <http://codinuum.com/ontologies/2012/10/java-entity#>
PREFIX c:    <http://codinuum.com/ontologies/2012/10/c-entity#>


INSERT {
  ?file ?p_mapped_to ?file_ .
}
WHERE {

  ?file ver:version ?ver ;
        ver:fileDigest ?d ;
        src:location ?loc .

  ?file_ ver:version ?ver_ ;
         ver:fileDigest ?d_ ;
         src:location ?loc_ .

  ?ver ver:next ?ver_ .

  FILTER NOT EXISTS {
    ?file chg:mappedTo ?file_ .
  }

  ?tdecl a java:TypeDeclaration ;
         src:inFile ?file ;
         chg:mappedTo ?tdecl_ .

  ?tdecl_ a java:TypeDeclaration ;
          src:inFile ?file_ .

  BIND (IF(?d = ?d_, chg:mappedEqTo, chg:mappedNeqTo) AS ?p_mapped_to)

}
