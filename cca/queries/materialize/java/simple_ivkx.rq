
DEFINE input:inference "ont.cpi"

PREFIX ent:  <http://codinuum.com/fact/entity/>
PREFIX rel:  <http://codinuum.com/fact/version/release/>
PREFIX ext:  <http://codinuum.com/fact/external/>
PREFIX src:  <http://codinuum.com/ontologies/2012/10/source-code-entity#>
PREFIX ver:  <http://codinuum.com/ontologies/2012/10/versioning#>
PREFIX chg:  <http://codinuum.com/ontologies/2012/10/primitive-change#>
PREFIX java: <http://codinuum.com/ontologies/2012/10/java-entity#>
PREFIX c:    <http://codinuum.com/ontologies/2012/10/c-entity#>

INSERT {
  ?ivk java:mayInvoke ?sig .
  ?ivk java:mayInvokeMethod ?meth .
}
WHERE {

  FILTER NOT EXISTS {
    ?ivk java:mayInvoke [] ;
         java:mayInvokeMethod [] .
  }

  VALUES ?nparams { 1 2 3 4 5 6 7 8 9 10 11 12 13 14 15 }

  {
    SELECT DISTINCT ?ename ?class ?meth ?sig ?ivk ?args ?nparams ?nargs ?ver
    WHERE {

      {
        SELECT DISTINCT ?ename ?class0 ?meth ?sig ?nparams
        WHERE {

          ?meth a java:MethodDeclaration ;
                java:inTypeDeclaration ?class0 ;
                java:fullyQualifiedName ?fqn ;
                java:signature ?sig0 ;
                java:name ?ename ;
                #src:child5 ?body ;
                java:nParameters ?nparams .

          BIND (CONCAT(?fqn, ?sig0) AS ?sig)

        } GROUP BY ?ename ?class0 ?meth ?sig ?nparams
      }

      {
        SELECT DISTINCT ?ivk ?class ?ename ?nargs ?args
        WHERE {

          ?ivk a java:Invocation ;
               a ?cat OPTION (INFERENCE NONE) ;
               java:inTypeDeclaration ?class ;
               java:name ?ename ;
               java:nArguments ?nargs ;
               src:child1 ?args .

          FILTER (?cat IN (java:SimpleMethodInvocation,java:SimpleMethodInvocationStatement))

        } GROUP BY ?ivk ?class ?ename ?nargs ?args
      }

      ?class ver:version ?ver .
      ?class0 ver:version ?ver .

      FILTER (EXISTS {
        ?class java:inTypeDeclaration ?class1 OPTION (TRANSITIVE,
                                                      T_DISTINCT,
                                                      T_SHORTEST_ONLY,
                                                      T_NO_CYCLES,
                                                      T_MIN(0)) .

        ?class1 java:subTypeOf ?class0 OPTION (TRANSITIVE,
                                               T_DISTINCT,
                                               T_SHORTEST_ONLY,
                                               T_NO_CYCLES,
                                               T_MIN(0)) .
      } || ?class = ?class0)

      ?args a java:Arguments .

    } GROUP BY ?ename ?class ?meth ?sig ?ivk ?args ?nparams ?nargs ?ver
  }

  FILTER NOT EXISTS {
    GRAPH <http://codinuum.com/ont/cpi> {
      ?p_childx rdfs:subPropertyOf src:child .
    }
    BIND (STRAFTER(STR(?p_childx), "child") AS ?x)
    BIND (fn:number(?x) AS ?xn)
    FILTER (?xn < ?nparams)

    BIND (IRI(CONCAT(STR(java:parameterTypeName), ?x)) AS ?p_param_ty_namex)
    BIND (IRI(CONCAT(STR(java:parameterTypeDims), ?x)) AS ?p_param_ty_dimsx)

    ?args ?p_childx ?ax .
    ?meth ?p_param_ty_namex ?ptynamex .
    OPTIONAL {
      ?meth ?p_param_ty_dimsx ?ptydimsx .
    }
    OPTIONAL {
      ?ax java:typeDims ?atydimsx .
    }

    FILTER ((NOT EXISTS {
      ?ax java:typeName ?atynamex .
      FILTER (?atynamex = ?ptynamex)
    } && NOT EXISTS {
      ?ptyx a java:TypeDeclaration ;
            ver:version ?ver ;
            java:fullyQualifiedName ?ptynamex .
      ?ax java:ofReferenceType ?atyx .
      ?atyx java:subTypeOf ?ptyx OPTION (TRANSITIVE, T_DISTINCT, T_MIN(0)) ;
            ver:version ?ver .
    } && ?ptynamex != "java.lang.Object") ||
    (BOUND(?atydimsx) && ?atydimsx != ?ptydimsx))
  }

  FILTER (?nparams = ?nargs || (?nargs = ?nparams - 1 && EXISTS {
    ?meth java:isVariableArityMethod "true" .
  }) || (?nparams < ?nargs && EXISTS {
    ?meth java:isVariableArityMethod "true" .
  } && NOT EXISTS {

    BIND (STR(?nparams-1) AS ?lastp)
    BIND (IRI(CONCAT(STR(java:parameterTypeName), ?lastp)) AS ?p_param_ty_namex)
    BIND (IRI(CONCAT(STR(java:parameterTypeDims), ?lastp)) AS ?p_param_ty_dimsx)

    GRAPH <http://codinuum.com/ont/cpi> {
      ?p_childx rdfs:subPropertyOf src:child .
    }
    BIND (STRAFTER(STR(?p_childx), "child") AS ?x)
    BIND (fn:number(?x) AS ?xn)
    FILTER (?xn >= ?nparams && ?xn < ?nargs)

    ?args ?p_childx ?ax .
    ?meth ?p_param_ty_namex ?ptynamex .
    OPTIONAL {
      ?meth ?p_param_ty_dimsx ?ptydimsx .
    }
    OPTIONAL {
      ?ax java:typeDims ?atydimsx .
    }

    FILTER ((NOT EXISTS {
      ?ax java:typeName ?atynamex .
      FILTER (?atynamex = ?ptynamex)
    } && NOT EXISTS {
      ?ptyx a java:TypeDeclaration ;
            ver:version ?ver ;
            java:fullyQualifiedName ?ptynamex .
      ?ax java:ofReferenceType ?atyx .
      ?atyx java:subTypeOf ?ptyx OPTION (TRANSITIVE, T_DISTINCT, T_MIN(0)) ;
            ver:version ?ver .
    } && ?ptynamex != "java.lang.Object") ||
    (BOUND(?atydimsx) && ?atydimsx != ?ptydimsx))
  }))

}
