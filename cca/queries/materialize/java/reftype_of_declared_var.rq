
DEFINE input:inference "ont.cpi"

PREFIX ent:  <http://codinuum.com/fact/entity/>
PREFIX rel:  <http://codinuum.com/fact/version/release/>
PREFIX ext:  <http://codinuum.com/fact/external/>
PREFIX src:  <http://codinuum.com/ontologies/2012/10/source-code-entity#>
PREFIX ver:  <http://codinuum.com/ontologies/2012/10/versioning#>
PREFIX chg:  <http://codinuum.com/ontologies/2012/10/primitive-change#>
PREFIX java: <http://codinuum.com/ontologies/2012/10/java-entity#>
PREFIX c:    <http://codinuum.com/ontologies/2012/10/c-entity#>


INSERT {
  ?var java:ofReferenceType ?CorI ;
       java:typeName ?tyname .
}
WHERE {

  {
    SELECT DISTINCT ?var ?vname ?dtor ?src
    WHERE {

      ?var a java:Name ;
           java:inTypeDeclaration ?class ;
           java:name ?vname ;
           java:declaredBy ?dtor .

      ?class a java:TypeDeclaration ;
             src:inSourceTree ?src .

    } GROUP BY ?var ?vname ?dtor ?src
  }

  {
    ?dtor a java:VariableDeclarator ;
          java:inVariableDeclaration ?decl .
    ?ty src:parent ?decl .
  }
  UNION
  {
    ?dtor a java:Resource ;
          src:child1 ?ty .
  }

  ?ty a java:ReferenceType ;
      java:refersToDeclaration ?CorI ;
      java:name ?tyname .

}
