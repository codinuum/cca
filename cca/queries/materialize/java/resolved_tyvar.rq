
DEFINE input:inference "ont.cpi"

PREFIX ent:  <http://codinuum.com/fact/entity/>
PREFIX rel:  <http://codinuum.com/fact/version/release/>
PREFIX ext:  <http://codinuum.com/fact/external/>
PREFIX src:  <http://codinuum.com/ontologies/2012/10/source-code-entity#>
PREFIX ver:  <http://codinuum.com/ontologies/2012/10/versioning#>
PREFIX chg:  <http://codinuum.com/ontologies/2012/10/primitive-change#>
PREFIX java: <http://codinuum.com/ontologies/2012/10/java-entity#>
PREFIX c:    <http://codinuum.com/ontologies/2012/10/c-entity#>


INSERT {
  ?rty java:name ?btname .
}
WHERE {

  {
    SELECT DISTINCT ?class ?fqn ?typarams
    WHERE {

      ?class a java:TypeDeclaration ;
             java:fullyQualifiedName ?fqn .

      ?class src:child0/src:child1 ?typarams .

      ?typarams a java:TypeParameters .

    } GROUP BY ?class ?fqn ?typarams
  }

  ?typaram a java:TypeParameter;
           java:name ?tpname ;
           src:parent ?typarams ;
           src:child1 ?tybound .

  ?tybound a java:TypeBound ;
           src:child0 ?bty .

  ?bty java:name ?btname .

  ?rty a java:ReferenceType ;
       java:inTypeDeclaration ?class ;
       java:name ?tpname .

}
