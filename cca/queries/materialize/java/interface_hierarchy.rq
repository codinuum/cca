
DEFINE input:inference "ont.cpi"

PREFIX ent:  <http://codinuum.com/fact/entity/>
PREFIX rel:  <http://codinuum.com/fact/version/release/>
PREFIX ext:  <http://codinuum.com/fact/external/>
PREFIX src:  <http://codinuum.com/ontologies/2012/10/source-code-entity#>
PREFIX ver:  <http://codinuum.com/ontologies/2012/10/versioning#>
PREFIX chg:  <http://codinuum.com/ontologies/2012/10/primitive-change#>
PREFIX java: <http://codinuum.com/ontologies/2012/10/java-entity#>
PREFIX c:    <http://codinuum.com/ontologies/2012/10/c-entity#>


INSERT {
  ?subinterface java:subInterfaceOf ?interface .
}
WHERE {

  {
    SELECT DISTINCT ?ty ?subinterface ?ver ?fqn0 ?tname ?tname_e
    WHERE {

      ?subinterface a java:InterfaceDeclaration ;
                    java:fullyQualifiedName ?fqn0 ;
                    ver:version ?ver .

      ?ei a java:ExtendsInterfaces ;
          java:inTypeDeclaration ?subinterface .

      ?ty a java:ReferenceType ;
          java:name ?tname ;
          src:parent ?ei .

      BIND (STRBEFORE(?tname, "<") AS ?tname_e)

    } GROUP BY ?ty ?subinterface ?ver ?fqn0 ?tname ?tname_e
  }

  ?interface a java:TypeDeclaration ;
         src:inFile ?file ;
         ver:version ?ver .
  {
    ?interface java:fullyQualifiedName ?tname .
  }
  UNION
  {
     ?interface java:fullyQualifiedName ?tname_e .
  }

}
