
DEFINE input:inference "ont.cpi"

PREFIX ent:  <http://codinuum.com/fact/entity/>
PREFIX rel:  <http://codinuum.com/fact/version/release/>
PREFIX ext:  <http://codinuum.com/fact/external/>
PREFIX src:  <http://codinuum.com/ontologies/2012/10/source-code-entity#>
PREFIX ver:  <http://codinuum.com/ontologies/2012/10/versioning#>
PREFIX chg:  <http://codinuum.com/ontologies/2012/10/primitive-change#>
PREFIX java: <http://codinuum.com/ontologies/2012/10/java-entity#>
PREFIX c:    <http://codinuum.com/ontologies/2012/10/c-entity#>

INSERT {
  ?x java:name ?fqn .
}
WHERE {

  {
    SELECT DISTINCT ?ver ?tdecl ?fqn ?cname ?ename
    WHERE {

      ?econst a java:EnumConstant ;
              java:name ?ename ;
              java:inEnum ?enum .

      ?enum a java:EnumDeclaration ;
            java:name ?cname ;
            java:fullyQualifiedName ?tyname ;
            java:inTypeDeclaration ?tdecl .

      ?tdecl ver:version ?ver .

      BIND(CONCAT(?tyname, ".", ?ename) AS ?fqn)

    } GROUP BY ?ver ?tdecl ?fqn ?cname ?ename
  }

  ?x java:name ?xname ;
     java:inTypeDeclaration ?tdecl0 .

  FILTER (CONCAT(?cname, ".", ?ename) = ?xname)

  ?tdecl0 ver:version ?ver ;
          java:subTypeOf* ?tdecl .

}
