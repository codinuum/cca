
DEFINE input:inference "ont.cpi"

PREFIX ent:  <http://codinuum.com/fact/entity/>
PREFIX rel:  <http://codinuum.com/fact/version/release/>
PREFIX ext:  <http://codinuum.com/fact/external/>
PREFIX src:  <http://codinuum.com/ontologies/2012/10/source-code-entity#>
PREFIX ver:  <http://codinuum.com/ontologies/2012/10/versioning#>
PREFIX chg:  <http://codinuum.com/ontologies/2012/10/primitive-change#>
PREFIX java: <http://codinuum.com/ontologies/2012/10/java-entity#>
PREFIX c:    <http://codinuum.com/ontologies/2012/10/c-entity#>

INSERT {
  ?new java:ofReferenceType ?tdecl .
}
WHERE {

  FILTER NOT EXISTS {
    ?new java:ofReferenceType [] .
  }

  {
    SELECT DISTINCT ?src ?tdecl ?mname
    WHERE {
      ?tdecl a java:TypeDeclaration ;
             src:inSourceTree ?src ;
             java:fullyQualifiedName ?fqn .

      BIND (CONCAT(?fqn, ".<init>") AS ?mname)
    } GROUP BY ?src ?tdecl ?mname
  }

  ?new a java:InstanceCreation ;
       java:inTypeDeclaration/src:inSourceTree ?src ;
       java:name ?mname .

}
