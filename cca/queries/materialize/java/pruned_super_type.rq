
DEFINE input:inference "ont.cpi"

PREFIX ent:  <http://codinuum.com/fact/entity/>
PREFIX rel:  <http://codinuum.com/fact/version/release/>
PREFIX ext:  <http://codinuum.com/fact/external/>
PREFIX src:  <http://codinuum.com/ontologies/2012/10/source-code-entity#>
PREFIX ver:  <http://codinuum.com/ontologies/2012/10/versioning#>
PREFIX chg:  <http://codinuum.com/ontologies/2012/10/primitive-change#>
PREFIX java: <http://codinuum.com/ontologies/2012/10/java-entity#>
PREFIX c:    <http://codinuum.com/ontologies/2012/10/c-entity#>


INSERT {
  ?ty chg:prunedFrom ?src_ .
}
WHERE {

  ?src a src:SourceTree ;
       ver:version ?ver .

  ?file a src:File ;
        src:inSourceTree ?src ;
        chg:prunedFrom ?src_ .

  ?class a java:TypeDeclaration ;
         src:inFile ?file .

  ?x a ?cat ;
     src:parent/src:parent ?class .

  FILTER (?cat IN (java:Extends, java:ExtendsInterfaces, java:Implements))

  ?ty a java:ReferenceType ;
      src:parent ?x .

  FILTER NOT EXISTS {
    ?class chg:mappedTo [] .
  }

}
