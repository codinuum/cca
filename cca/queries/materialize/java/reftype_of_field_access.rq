
DEFINE input:inference "ont.cpi"

PREFIX ent:  <http://codinuum.com/fact/entity/>
PREFIX rel:  <http://codinuum.com/fact/version/release/>
PREFIX ext:  <http://codinuum.com/fact/external/>
PREFIX src:  <http://codinuum.com/ontologies/2012/10/source-code-entity#>
PREFIX ver:  <http://codinuum.com/ontologies/2012/10/versioning#>
PREFIX chg:  <http://codinuum.com/ontologies/2012/10/primitive-change#>
PREFIX java: <http://codinuum.com/ontologies/2012/10/java-entity#>
PREFIX c:    <http://codinuum.com/ontologies/2012/10/c-entity#>


INSERT {
  ?x java:ofReferenceType ?TD ;
     java:typeName ?tyname ;
     java:typeDims ?dims ;
     java:declaredBy ?dtor .
}
WHERE {

  FILTER NOT EXISTS {
    ?x java:ofReferenceType [] .
  }

  {
    SELECT DISTINCT ?tdecl0 ?fname ?TD ?dtor ?tyname ?dims
    WHERE {

      ?field a java:FieldDeclaration ;
             src:child1 ?ty ;
             src:child2 ?dtor ;
             java:inTypeDeclaration ?tdecl0 .

      ?ty a java:ReferenceType ;
          src:parent ?field ;
          java:name ?tyname ;
          java:refersToDeclaration ?TD .

      ?dtor a java:VariableDeclarator ;
            src:parent ?field ;
            java:name ?fname .

      OPTIONAL {
        ?ty java:dimensions ?dims .
      }

    } GROUP BY ?tdecl0 ?fname ?TD ?dtor ?tyname ?dims
  }

  # ?tdecl (java:inTypeDeclaration|java:subTypeOf) ?tdecl0 OPTION (TRANSITIVE,
  #                                                                T_DISTINCT,
  #                                                                T_SHORTEST_ONLY,
  #                                                                T_NO_CYCLES,
  #                                                                T_MIN(0)) .

  {
    SELECT DISTINCT ?x ?fname ?tdecl
    WHERE {

      ?x a java:FieldAccess ;
         java:name ?fname ;
         src:child0 ?e .

      ?e a java:Expression ;
         #java:inTypeDeclaration/src:inSourceTree ?src ;
         java:ofReferenceType ?tdecl .

    } GROUP BY ?x ?fname ?tdecl
  }

  ?tdecl0 src:inSourceTree ?src .
  ?tdecl src:inSourceTree ?src .

  FILTER EXISTS {
    ?tdecl java:inTypeDeclaration ?tdecl1 OPTION (TRANSITIVE,
                                                  T_DISTINCT,
                                                  T_SHORTEST_ONLY,
                                                  T_NO_CYCLES,
                                                  T_MIN(0)) .

    ?tdecl1 java:subTypeOf ?tdecl0 OPTION (TRANSITIVE,
                                           T_DISTINCT,
                                           T_SHORTEST_ONLY,
                                           T_NO_CYCLES,
                                           T_MIN(0)) .
  }

}
